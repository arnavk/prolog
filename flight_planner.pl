%% Facts for all the flights
flight(ba58, singapore, london, 2310, 520, monday).
flight(ba58, singapore, london, 2310, 520, wednesday).
flight(ba58, singapore, london, 2310, 520, thursday).
flight(ba58, singapore, london, 2310, 520, saturday).

flight(ba24, london, singapore, 1000, 1600, monday).
flight(ba24, london, singapore, 1000, 1600, wednesday).
flight(ba24, london, singapore, 1000, 1600, thursday).
flight(ba24, london, singapore, 1000, 1600, saturday).

flight(ba4732, london, edinburgh, 0940, 1050, monday).
flight(ba4732, london, edinburgh, 0940, 1050, tuesday).
flight(ba4732, london, edinburgh, 0940, 1050, wednesday).
flight(ba4732, london, edinburgh, 0940, 1050, thursday).
flight(ba4732, london, edinburgh, 0940, 1050, friday).
flight(ba4732, london, edinburgh, 0940, 1050, saturday).
flight(ba4732, london, edinburgh, 0940, 1050, sunday).

flight(ba4735, london, edinburgh, 1140, 1250, monday).
flight(ba4735, london, edinburgh, 1140, 1250, tuesday).
flight(ba4735, london, edinburgh, 1140, 1250, wednesday).
flight(ba4735, london, edinburgh, 1140, 1250, thursday).
flight(ba4735, london, edinburgh, 1140, 1250, friday).
flight(ba4735, london, edinburgh, 1140, 1250, saturday).
flight(ba4735, london, edinburgh, 1140, 1250, sunday).

flight(ba4822, london, edinburgh, 1840, 1950, monday).
flight(ba4822, london, edinburgh, 1840, 1950, tuesday).
flight(ba4822, london, edinburgh, 1840, 1950, wednesday).
flight(ba4822, london, edinburgh, 1840, 1950, thursday).
flight(ba4822, london, edinburgh, 1840, 1950, friday).

flight(ba4733, edinburgh, london, 0830, 0940, monday).
flight(ba4733, edinburgh, london, 0830, 0940, tuesday).
flight(ba4733, edinburgh, london, 0830, 0940, wednesday).
flight(ba4733, edinburgh, london, 0830, 0940, thursday).
flight(ba4733, edinburgh, london, 0830, 0940, friday).
flight(ba4733, edinburgh, london, 0830, 0940, saturday).
flight(ba4733, edinburgh, london, 0830, 0940, sunday).

flight(ba4736, edinburgh, london, 1340, 1450, monday). 
flight(ba4736, edinburgh, london, 1340, 1450, tuesday).
flight(ba4736, edinburgh, london, 1340, 1450, wednesday).
flight(ba4736, edinburgh, london, 1340, 1450, thursday).
flight(ba4736, edinburgh, london, 1340, 1450, friday).
flight(ba4736, edinburgh, london, 1340, 1450, saturday).
flight(ba4736, edinburgh, london, 1340, 1450, sunday).

flight(ba4833, edinburgh, london, 1940, 2050, monday).
flight(ba4833, edinburgh, london, 1940, 2050, tuesday).
flight(ba4833, edinburgh, london, 1940, 2050, wednesday).
flight(ba4833, edinburgh, london, 1940, 2050, thursday).
flight(ba4833, edinburgh, london, 1940, 2050, friday).
flight(ba4833, edinburgh, london, 1940, 2050, sunday).

flight(ba614, london, greece, 0910, 1245, monday).
flight(ba614, london, greece, 0910, 1245, tuesday).
flight(ba614, london, greece, 0910, 1245, wednesday).
flight(ba614, london, greece, 0910, 1245, thursday).
flight(ba614, london, greece, 0910, 1245, friday).
flight(ba614, london, greece, 0910, 1245, saturday).
flight(ba614, london, greece, 0910, 1245, sunday).

flight(sr805, london, greece, 1445, 1820, monday).
flight(sr805, london, greece, 1445, 1820, tuesday).
flight(sr805, london, greece, 1445, 1820, wednesday).
flight(sr805, london, greece, 1445, 1820, thursday).
flight(sr805, london, greece, 1445, 1820, friday).
flight(sr805, london, greece, 1445, 1820, saturday).
flight(sr805, london, greece, 1445, 1820, sunday).

flight(ba613, greece, london, 0900, 1140, monday).
flight(ba613, greece, london, 0900, 1140, tuesday).
flight(ba613, greece, london, 0900, 1140, wednesday).
flight(ba613, greece, london, 0900, 1140, thursday).
flight(ba613, greece, london, 0900, 1140, friday).
flight(ba613, greece, london, 0900, 1140, saturday).

flight(sr806, greece, london, 1610, 1855, monday).
flight(sr806, greece, london, 1610, 1855, tuesday).
flight(sr806, greece, london, 1610, 1855, wednesday).
flight(sr806, greece, london, 1610, 1855, thursday).
flight(sr806, greece, london, 1610, 1855, friday).
flight(sr806, greece, london, 1610, 1855, sunday).

flight(ba510, london, paris, 0830, 1030, monday).
flight(ba510, london, paris, 0830, 1030, tuesday).
flight(ba510, london, paris, 0830, 1030, wednesday).
flight(ba510, london, paris, 0830, 1030, thursday).
flight(ba510, london, paris, 0830, 1030, friday).
flight(ba510, london, paris, 0830, 1030, saturday).
flight(ba510, london, paris, 0830, 1030, sunday).

flight(az459, london, paris, 1310, 1510, monday).
flight(az459, london, paris, 1310, 1510, tuesday).
flight(az459, london, paris, 1310, 1510, wednesday).
flight(az459, london, paris, 1310, 1510, thursday).
flight(az459, london, paris, 1310, 1510, friday).
flight(az459, london, paris, 1310, 1510, saturday).
flight(az459, london, paris, 1310, 1510, sunday).

flight(ba511, paris, london, 0910, 1020, monday).
flight(ba511, paris, london, 0910, 1020, tuesday).
flight(ba511, paris, london, 0910, 1020, wednesday).
flight(ba511, paris, london, 0910, 1020, thursday).
flight(ba511, paris, london, 0910, 1020, friday).
flight(ba511, paris, london, 0910, 1020, saturday).
flight(ba511, paris, london, 0910, 1020, sunday).

flight(az460, paris, london, 1220, 1330, monday).
flight(az460, paris, london, 1220, 1330, tuesday).
flight(az460, paris, london, 1220, 1330, wednesday).
flight(az460, paris, london, 1220, 1330, thursday).
flight(az460, paris, london, 1220, 1330, friday).
flight(az460, paris, london, 1220, 1330, saturday).
flight(az460, paris, london, 1220, 1330, sunday).

flight(jp322, paris, rome, 1130, 1240, tuesday).
flight(jp322, paris, rome, 1130, 1240, wednesday).
flight(jp322, paris, rome, 1130, 1240, thursday).

flight(jp323, rome, paris, 1130, 1240, tuesday).
flight(jp323, rome, paris, 1130, 1240, wednesday).
flight(jp323, rome, paris, 1130, 1240, thursday).

flight(fs619, rome, greece, 1440, 1630, monday).
flight(fs619, rome, greece, 1440, 1630, wednesday).
flight(fs619, rome, greece, 1440, 1630, thursday).
flight(fs619, rome, greece, 1440, 1630, friday).

flight(fs620, greece, rome, 1100, 1310, monday).
flight(fs620, greece, rome, 1100, 1310, wednesday).
flight(fs620, greece, rome, 1100, 1310, thursday).
flight(fs620, greece, rome, 1100, 1310, friday).


%% Facts for the days
day(monday, 1).
day(tuesday, 2).
day(wednesday, 3).
day(thursday, 4).
day(friday, 5).
day(saturday, 6).
day(sunday, 7).


%% Facts for choice of location
location(1, singapore).
location(2, london).
location(3, edinburgh).
location(4, greece).
location(5, paris).
location(6, rome).

%% Gets the absolute time of the day. 
%% It is the numberof minutes passed since 0000HRS on Monday till "Time_of_day" on "Day".
absolute_time(Time_of_day, Day, Abs_Time):- 
	day(Day,X),
	Abs_Time is (((Time_of_day//100)*60)+(mod(Time_of_day,100)) + (X-1)*1440).

%% Finds the time difference (in minutes) between "Start_time" and "Start_Day". 
%% It does calculations wrapping around weeks so 0000HRS on monday is 1 minute after 2359 on sunday. 
time_difference(Start_time, Start_Day, End_time, End_day, Time_Diff):- 
	absolute_time(Start_time, Start_Day, Start_Abs), 
	absolute_time(End_time, End_day, End_Abs), 
	Diff is (End_Abs - Start_Abs), 
	Time_Diff is (mod(Diff, 10080)).

%% Retrieves the correct flight based on the parameters.
%% It also finds it correct arrival day. (Taking care of the special case of ba58)
find_flight(Number, Origin, Destination, Start_time, End_time, Start_Day, End_day):-
	flight(Number, Origin, Destination, Start_time, End_time, Start_Day),
	absolute_time(Start_time, Start_Day, Start_Abs), 
	absolute_time(End_time, Start_Day, End_Abs), 
	Diff is (End_Abs - Start_Abs),
	fix_end_day(Diff, Start_Day, End_day).

%% These are helper functions to correct the arrival day
fix_end_day(Diff, Old_Day, New_Day):- 
	Diff>=0, 
	day(Old_Day, Old_Day_int), 
	day(New_Day, Old_Day_int).

fix_end_day(Diff, Old_Day, New_Day):- 
	Diff<0, 
	day(Old_Day, Old_Day_int),
	New_Day_int is (Old_Day_int+1),
	day(New_Day, New_Day_int).

%% So as to not use the member funtion provided in some prolog libraries.
%% This makes the code self sufficient
member_of(X,[X|_]):- !.
member_of(X,[_|T]) :- member_of(X,T).

%% CASE 1: Next node is the destination
route(Origin, Destination, [[Flightno, Day]], Visited):-
    find_flight(Flightno, Origin, Destination, _, _, Day, _),
    not(member_of(Destination,Visited)).

%% CASE 2: 
route(Origin, Destination, [[Flightno, New_Day]|Route], Visited):-
    find_flight(Flightno, Origin, Transit, _, _, New_Day, _),
    not(member_of(Transit,Visited)),
    route(Transit, Destination, Route, [Transit|Visited]).

%% Query Part
plan_route(Origin_City, Destination_City, Route):- 
    route(Origin_City, Destination_City, Route, [Origin_City]).

%% Processes a flight node, which is used in routes and finds the corresponding flight.
process_flight_node([Number, Day|_], Start_time, End_time, Day, Flight_Arrival_Day):-
	find_flight(Number, _, _, Start_time, End_time, Day, Flight_Arrival_Day).


%% This function is used calcuate the total travel time for a route, recursively.
calculate_route_time([First_Flight|Route], Time):-	
	process_route([First_Flight|Route], 0, Time).


%% This function is used to calcuate the total time for a route given a start day, recursively.
calculate_route_time_from_specified_time([First_Flight|Route], Start_time, Start_Day, Time):-	
	process_flight_node(First_Flight, FF_Start, _, FF_Day, _),
	time_difference(Start_time, Start_Day, FF_Start, FF_Day, InitialWait),
	process_route([First_Flight|Route], 0, Route_time),
	Time is (Route_time + InitialWait).

%% Prepares a list of routes between the "Origin" and "Destination"
list_all_routes(Origin, Destination, RouteList):-
	findall(Route, plan_route(Origin, Destination, Route), RouteList).

%% Processes a route and computes the total time for it, recursively.
process_route([First_Flight, Second_Flight|Route], Time, Result):-
	process_flight_node(First_Flight, FF_Start, FF_End, FF_Day, FF_Arrival_day),
	process_flight_node(Second_Flight, SF_Start, _, SF_Day, _),
	time_difference(FF_Start, FF_Day, FF_End, FF_Arrival_day, FF_Duration),
	time_difference(FF_End, FF_Arrival_day, SF_Start, SF_Day, Transit_Time),
	New_time is (Time + FF_Duration + Transit_Time),
	process_route([Second_Flight|Route], New_time, Result).

process_route([First_Flight, Second_Flight|[]], Time, Result):-
	process_flight_node(First_Flight, FF_Start, FF_End, FF_Day, FF_Arrival_day),
	process_flight_node(Second_Flight, SF_Start, SF_End, SF_Day, SF_Arrival_day),
	time_difference(FF_Start, FF_Day, FF_End, FF_Arrival_day, FF_Duration),
	time_difference(SF_Start, SF_Day, SF_End, SF_Arrival_day, SF_Duration),
	time_difference(FF_End, FF_Arrival_day, SF_Start, SF_Day, Transit_Time),
	New_time is (Time + FF_Duration + Transit_Time + SF_Duration),
	process_route([], New_time, Result).

%% Last flight node
process_route([First_Flight|[]], Time, Result):-
	process_flight_node(First_Flight, FF_Start, FF_End, FF_Day, FF_Arrival_day),
	time_difference(FF_Start, FF_Day, FF_End, FF_Arrival_day, FF_Duration),
	Result is (Time + FF_Duration).
	
%% These functions find the minimum value in a list of numbers	
min_in_list([Min],Min).

min_in_list([H,K|T],M) :-
    H =< K,
    min_in_list([H|T],M).

min_in_list([H,K|T],M) :-
    H > K,
    min_in_list([K|T],M).

%% These functions produce a list of travel times for "Route".
get_travel_time_list([], List, List).

get_travel_time_list([Head|Route], List, Result):-
	calculate_route_time(Head, Time),
	get_travel_time_list(Route, [Time|List], Result).


%% These functions produce a list of total time (starting on a given day) for "Route".
get_total_time_list([], _, List, List).

get_total_time_list([Head|Route], Day, List, Result):-
	calculate_route_time_from_specified_time(Head, 0000, Day, Time),
	get_total_time_list(Route, Day, [Time|List], Result).

%% Finds the fastest route from "Origin" to "Destination" on or after "Day"
find_fastest_route(Origin, Destination, Day):-
	list_all_routes(Origin, Destination, RouteList),
	get_travel_time_list(RouteList, [], TravelList),
	get_total_time_list(RouteList, Day, [], Total_Time_List),
	min_in_list(TravelList, MinTravelTime),
	min_in_list(Total_Time_List, MinTotalTime),
	extract_fastest_route_from_list(RouteList, Day, MinTravelTime, MinTotalTime).

%% Finds the fastest "Route" (Each Route is between Origin and Destination)
extract_fastest_route_from_list([Route|Tail], Day, MinTravelTime, MinTotalTime):-
	calculate_route_time(Route, Travel_Time),
	calculate_route_time_from_specified_time(Route, 0000, Day, Total_Time),
	Travel_Time = MinTravelTime,
	Total_Time = MinTotalTime,
	print_route(Route),
	write('     Total Travel Time: '),
	print_time_difference(Travel_Time),
	write('\n\n'),
	write('Have a pleasant journey!'),
	nl,write('---------------------------------------------'),nl,nl,
	extract_fastest_route_from_list([], _, 0, 0); 
	extract_fastest_route_from_list(Tail, Day, MinTravelTime, MinTotalTime).

extract_fastest_route_from_list([], _, _, _):-false.
	
%% Helper function to print the fastest "Route".
print_route([[Number, Day|_], Second_Flight|Route]):-
	find_flight(Number, Origin, Destination, Start_time, End_time, Day, Flight_Arrival_Day),
	write('     Flight Number: '), 
	write(Number), 
	write(' ('),
	write(Origin), 
	write(' to '),
	write(Destination), 
	write(')\n'), 
	write('		Departure:'), 
	print_time(Start_time),
	write(' on '),
	write(Day), 
	write('\n'),
	write('		Arrival  :'),
	print_time(End_time),
	write(' on '),
	write(Flight_Arrival_Day),
	write('\n'),
	time_difference(Start_time, Day, End_time, Flight_Arrival_Day, Duration),
	write('		Duration :'),
	print_time_difference(Duration),
	write('\n'),
	process_flight_node(Second_Flight, SF_Start, _, SF_Day, _),
	time_difference(End_time, Flight_Arrival_Day, SF_Start, SF_Day, Transit_Time),
	write('\n'),
	write('     Wating Time: '),
	print_time_difference(Transit_Time),
	write('\n\n'),
	print_route([Second_Flight|Route]).

print_route([[Number, Day|_]|[]]):-
	find_flight(Number, Origin, Destination, Start_time, End_time, Day, Flight_Arrival_Day),
	write('     Flight Number: '), 
	write(Number), 
	write(' ('),
	write(Origin), 
	write(' to '),
	write(Destination), 
	write(')\n'), 
	write('		Departure:'), 
	print_time(Start_time),
	write(' on '),
	write(Day), 
	write('\n'),
	write('		Arrival  :'),
	print_time(End_time),
	write(' on '),
	write(Flight_Arrival_Day),
	write('\n'),
	time_difference(Start_time, Day, End_time, Flight_Arrival_Day, Duration),
	write('		Duration :'),
	print_time_difference(Duration),
	write('\n'),
	write('\n'),
	print_route([]).	

print_route([]).


%% These funtions are used to print a time (in minutes) with proper formatting.
print_time(Time):- Time<1000, write('0'), write(Time), write('HRS').
print_time(Time):- Time>=1000,write(Time), write('HRS').
print_time_difference(Minutes):-
	Hours is Minutes//60,
	print_hours(Hours),
	Mins is mod(Minutes,60),
	print_minutes(Mins).

print_hours(Hours):- Hours>1, write(Hours), write(' hours ').
print_hours(Hours):- Hours=1, write(Hours), write(' hour ').
print_hours(Hours):- Hours<1, write('').

print_minutes(Minutes):- Minutes>1, write(Minutes), write(' minutes').
print_minutes(Minutes):- Minutes=1, write(Minutes), write(' minute').
print_minutes(Minutes):- Minutes<1, write('').

%% This is the main method with handles user input and computes the optimal routes.
main :- nl, write('---------------------------------------------'),
	nl, write('	     Welcome to Flight Planner!'),
	nl, write('	Let us help you plan your travel!'),
	nl, write('---------------------------------------------'),
	nl, write('Please pick your Origin City (Enter its number followed by a period (.):-'),
	nl, write('1. Singapore'),
	nl, write('2. London'),
	nl, write('3. Edinburgh'),
	nl, write('4. Greece'),
	nl, write('5. Paris'),
	nl, write('6. Rome'),nl,
	
	%% Reading Origin
	read(Origin_int),
	location(Origin_int, Origin),

	nl,write('Please pick your Destination City (Enter its number followed by a period (.):-'),
	nl,write('1. Singapore'),
	nl,write('2. London'),
	nl,write('3. Edinburgh'),
	nl,write('4. Greece'),
	nl,write('5. Paris'),
	nl,write('6. Rome'),nl,
	
	%% Reading Destination
	read(Destination_int),
	location(Destination_int, Destination),

	nl,write('Please enter the day you want to travel on! (Enter its number followed by a period (.):-'),
	nl,write('1. Monday'),
	nl,write('2. Tuesday'),
	nl,write('3. Wednesday'),
	nl,write('4. Thursday'),
	nl,write('5. Friday'),
	nl,write('6. Saturday'),
	nl,write('7. Sunday'),nl,
	
	%% Reading Day
	read(Day_int),
	day(Day, Day_int),

	nl,write('---------------------------------------------'),
	nl,write('Searching for a flight from '),
	write(Origin), write(' to '),write(Destination),
	nl,write('		 Please Wait..'),
	nl,write('---------------------------------------------'),nl,
	nl, 

	%% Finding the best route.
	find_fastest_route(Origin,Destination,Day).